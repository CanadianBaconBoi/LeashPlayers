plugins {
    id 'dev.architectury.loom' version '0.12.0-SNAPSHOT'
}

group = project.mod_group
archivesBaseName = project.mod_name

version = project.mod_version

loom {
    forge {
        mixinConfigs = [
            'leashplayers.mixins.json'
        ]

        dataGen {
            mod project.mod_name
        }
    }

    launches {
        data {
            arg '--existing', file('src/main/resources').absolutePath
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
    forge "net.minecraftforge:forge:${project.forge_version}"
}

compileJava {
    options.release = 8
    options.encoding = 'UTF-8'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

jar {
    manifest {
        attributes([
            'Specification-Title'     : project.mod_name,
            'Specification-Vendor'    : 'luavixen',
            'Specification-Version'   : '1',
            'Implementation-Title'    : 'Leash Players',
            'Implementation-Version'  : project.mod_version,
            'Implementation-Vendor'   : 'luavixen',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    inputs.property 'version', project.mod_version

    filesMatching('META-INF/mods.toml') {
        expand 'version': project.mod_version
    }
}
